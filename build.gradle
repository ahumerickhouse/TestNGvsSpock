apply plugin: 'groovy'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile("org.codehaus.groovy:groovy-all:2.1.6")
    testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
        exclude module:"groovy-all"
    }
    testCompile "org.testng:testng:6.8"
}

task "testNg"(type: Test) {
    outputs.upToDateWhen { false }
    //Use and set the TestNG Options
    useTestNG() {
        //Use ReportNG reporting instead of TestNG reporting

        //Use TestNG reporting
        useDefaultListeners true

        //Sets a unique output directory for TestNG while keeping Gradle's reporting in tact.
        options.outputDirectory = new File("${buildDir}/reports/TestNG")

        //Parallel tests
        setMaxParallelForks Runtime.runtime.availableProcessors()

        //Set groups to run, exclude broken unless broken is specified
        if(project.hasProperty("testNG.group")){
            includeGroups project.getProperty("testNG.group")
            if(project.getProperty("testNG.group") != "broken") {
                excludeGroups "broken"
            }
        }
    }

    //Display what test is being executed
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    //Execute all tests regardless of failure
    setIgnoreFailures true
}

task "spock"(type: Test) {
    setMaxParallelForks Runtime.runtime.availableProcessors()
}
